buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-plugin-publish"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.codehaus.gpars:gpars:1.2.1"
    compile "org.grails:grails-console"
    profile "org.grails.profiles:plugin"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    testCompile "org.spockframework:spock-core"
    testCompile "cglib:cglib-nodep:3.3.0"
    testCompile "org.objenesis:objenesis:1.4"
    compile 'org.codehaus.groovy:groovy-json'
    compile 'com.rabbitmq:amqp-client:4.2.0'
    compile 'io.dropwizard.metrics:metrics-core:3.2.3'
}

bootRun {
    jvmArgs(
        '-Dspring.output.ansi.enabled=always',
        '-noverify',
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

grailsPublish {
    githubSlug = 'budjb/grails-rabbitmq-native'
    license {
        name = 'Apache-2.0'
    }
    title = "RabbitMQ Native Plugin"
    developers = [budjb: "Bud Byrd", olliefreeman: "Oliver Freeman", "aaron-brown": "Aaron Brown"]
    repo = "grails-plugins"

    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    websiteUrl = 'https://github.com/budjb/grails-rabbitmq-native'
    vcsUrl = 'https://github.com/budjb/grails-rabbitmq-native.git'
}

publishing {
    publications {
        maven(MavenPublication) {
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each { it.scope*.value = 'compile' }
            }
        }
    }
}
